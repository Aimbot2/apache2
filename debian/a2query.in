#! /usr/bin/perl

# a2query - Apache2 helper to retrieve configuration informations
# Copyright (C) 2012 Arno TÃ¶ll <debian@toell.net>
#
# This program is licensed at your choice under the terms of the GNU General
# Public License vserion 2+ or under the terms of the Apache Software License
# 2.0.
#
# For GPL-2+:
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301,
# USA.
#
# For ASF 2.0:
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


use feature "switch";
use strict;
use Getopt::Std;

=head1 NAME

a2query - retrieve runtime configuration from a local Apache 2 HTTP server

=cut


our $APACHE2 = "apache2ctl";
our $API = "__API__";
our $MODULE_DIR = "__MODULE_DIR__";
our $SERVER_VERSION = "__SERVER_VERSION__";
our $MPM = "invalid";
our $CONFIG_DIR = "/etc/apache2";
our @MODULES = ();
our @CONFS = ();
our @SITES =();
our @HELP = ();

=head1 SYNOPSIS

B<a2query>  [S<-m> [I<MODULE>]] [S<-s> [I<SITE>]] [S<-c> [I<CONF>]] [S<-a>] [S<-v>]
[S<-M>] [S<-d>] [S<-h>]

=head1 DESCRIPTION

B<a2query> is a program designed to retrieve configuration values from a locally
available Apache 2 HTTP web server. It was designed to be as robust as possible
by returning feasible values even if the Apache 2 syntax validator fails.

This program is primarily meant to be used from maintainer scripts.

=head1 OPTIONS

=over 4

=item S<-a>

Returns the Apache 2 "Module Magic Version" (API version) number, the server was
compiled with. The returned version does not contain any minor versions which
are known to be compatible with the major version returned.

=item S<-c> [I<CONF>]

Checks whether the configuration I<CONF> is enabled. If no argument was given,
all enabled configuration files are being returned. I<CONF> is evaluated as
L<perl>(1) compatible regular expression.

=item S<-h>

Displays a brief summary how the program can be called and exits.

=item S<-m> [I<MODULE>]

Checks whether the module I<MODULE> is enabled, The argument is interpreted in
the same way, as for configuration files queried by the S<-c> switch.

=item S<-M>

Returns the currently enabled Apache 2 MPM (Multi Processing Module).

=item S<-s> [I<SITE>]

Checks whether the module I<SITE> is enabled, The argument is interpreted in
the same way, as for configuration files queried by the S<-c> switch.


=item S<-v>

returns the currently installed Apache 2 HTTP server version

=back

=head1 SEE ALSO

L<apache2ctl>(8), L<apache2>(8), L<perl>(1)

=head1 AUTHOR

This manual and L<a2query> was written by Arno Toell <debian@toell.net>.

=cut

sub fail
{
	my $reason = shift;
	print STDERR "$reason\n";
	exit 1;
}

sub load_defaults
{
	my @out = `$APACHE2 -V`;
	return if $?;
	for (my $line; $line <= $#out; $line++)
	{
		if ($out[$line] =~ m/(Server version|Server MPM|Magic Number):\s+(.*?)$/)
		{
			my ($pattern, $value) = ($1, $2);
			if ($pattern =~ /version/)
			{
				$SERVER_VERSION = $value;
				$SERVER_VERSION =~ s/[^\d\.]//g;
			}
			elsif($pattern =~ /MPM/)
			{
				$MPM = $value;
			}
			elsif($pattern =~ /Magic/)
			{
				$API = $value;
				$API =~ s/\:\d+//;
			}
		}
	}

}

sub load_modules
{
	my $conf_dir = $CONFIG_DIR . "/mods-enabled";
	opendir(DIR, $conf_dir) || fail("$conf_dir: $!");
	while( readdir(DIR) )
	{
		my $file = $_;
		next if $file !~ m/\.load$/;
		$file =~ s/\.load//;
		if ($file =~ /mpm_(\w+)/)
		{
			$MPM = $1 if $MPM eq 'invalid';
			if(grep { $_ =~ m/^mpm_/ } @MODULES)
			{
				fail("There is more than one MPM loaded. Do not proceed due to undefined results");
			}
		}
		push @MODULES, $file;
	}
	closedir(DIR);
}


sub load_conf
{
	if ($#CONFS >= 0)
	{
		return;
	}
	my $conf_dir = $CONFIG_DIR . "/conf-enabled";
	opendir(DIR, $conf_dir) || fail("$conf_dir: $!");
	while( readdir(DIR) )
	{
		my $file = $_;
		next if $file !~ m/\.conf$/;
		push @CONFS, $file;
	}
	closedir(DIR);
}


sub load_sites
{
	if ($#SITES >= 0)
	{
		return;
	}
	my $conf_dir = $CONFIG_DIR . "/sites-enabled";
	opendir(DIR, $conf_dir) || fail("$conf_dir: $!");
	while( readdir(DIR) )
	{
		my $file = $_;
		next if $file !~ m/\.conf$/;
		push @SITES, $file;
	}
	closedir(DIR);
}


load_defaults();
load_modules();

my %opts;
my $help = 1;
getopt('m:s:c:havMd', \%opts);


push @HELP, ["m [MODULE]", "checks whether the module MODULE is enabled, lists all modules if no argument was given"];
if (exists $opts{'m'})
{
	--$help;
	my $matches = 0;
	if (not $opts{'m'})
	{
		$opts{'m'} = ".";
	}
	foreach my $module (grep { $_ =~ m/$opts{'m'}/ } @MODULES)
	{
		print("$module\n");
		$matches++;
	}
	if (!$matches)
	{
		fail("No module matches $opts{'m'}");
	}
}


push @HELP, ["s [SITE]", "checks whether the site SITE is enabled, lists all sites if no argument was given"];
if (exists $opts{'s'})
{
	--$help;
	load_sites();
	my $matches = 0;
	if (not $opts{'s'})
	{
		$opts{'s'} = ".";
	}
	foreach my $site (grep { $_ =~ m/$opts{'s'}/ } @SITES)
	{
		print("$site\n");
		$matches++;
	}
	if (!$matches)
	{
		fail("No site matches $opts{'s'}");
	}
}


push @HELP, ["c [CONF]", "checks whether the configuration CONF is enabled, lists all configurations if no argument was given"];
if (exists $opts{'c'})
{
	--$help;
	load_conf();
	my $matches = 0;
	if (not $opts{'c'})
	{
		$opts{'c'} = ".";
	}
	foreach my $conf (grep { $_ =~ m/$opts{'c'}/ } @CONFS)
	{
		print("$conf\n");
		$matches++;
	}
	if (!$matches)
	{
		fail("No conf matches $opts{'c'}");
	}
}


push @HELP, ["a", "returns the current Apache 2 module magic version"];
if (exists $opts{'a'})
{
	--$help;
	print("$API\n");
}


push @HELP, ["v", "returns the current Apache 2 version"];
if (exists $opts{'v'})
{
	--$help;
	print("$SERVER_VERSION\n");
}

push @HELP, ["M", "returns the enabled Apache 2 MPM"];
if (exists $opts{'M'})
{
	--$help;
	print("$MPM\n");
}

push @HELP, ["d", "returns the Apache 2 module directory"];
if (exists $opts{'d'})
{
	--$help;
	print("$MODULE_DIR\n");
}

push @HELP, ["h", "display this help"];
if (exists $opts{'h'} or $help == 1)
{
	my $usage = "$0 ";
	map { $usage .= "-$_->[0] " } @HELP;
	print("Usage: $usage\n");
	foreach my $switch (@HELP)
	{
		my ($switch, $description) = ($switch->[0], $switch->[1]);
		print("-$switch\t\t$description\n");
	}
	exit 0;
}


